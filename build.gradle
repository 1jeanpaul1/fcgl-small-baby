/*
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.9/userguide/tutorial_java_projects.html
 */

plugins {
    id 'java'
    id 'org.springframework.boot' version '2.0.4.RELEASE'
    id 'nu.studer.jooq' version '3.0.1' //https://github.com/etiennestuder/gradle-jooq-plugin
    //id 'org.flywaydb.flyway' version '5.1.4'//good for migration
}

apply plugin: 'idea'//IntelliJ
apply plugin: 'io.spring.dependency-management'//Provides Maven-like dependency management functionality

// Define the main class for the application
group = 'com.fcgl'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8
ext['jooq.version'] = '3.11.2'

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter and mavenCentral for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    mavenCentral()
    //references a local repository
    flatDir {
        dirs 'amazon-mws-v20090101-java-2016-09-21._CB476815245_/dist'
    }
}
configurations.implementation {
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
}
dependencies {
    // This dependency is found on compile classpath of this component and consumers.
    implementation 'org.springframework.boot:spring-boot-starter-web'

    jooqRuntime 'mysql:mysql-connector-java:8.0.12'
    implementation 'mysql:mysql-connector-java:8.0.12'

    //implementation 'org.springframework.boot:spring-boot-starter-security'//TODO: implement later

    implementation 'org.jooq:jooq'
    implementation 'org.jooq:jooq-meta:+'
    implementation 'org.jooq:jooq-meta-extensions:+'
    implementation 'org.jooq:jooq-codegen:+'
    implementation 'org.jooq:jooq-codegen-maven:+'

    implementation 'org.apache.httpcomponents:httpclient:4.5.6'//Helps with http requests
    implementation 'com.google.code.gson:gson:2.8.5'

    implementation name: 'MaWSJavaClientLibrary-1.1'//MWS Library

    // Testing Framework
    // Use JUnit 5 test framework
    testImplementation(
            'org.junit.jupiter:junit-jupiter-api:5.1.0'
    )
    testRuntimeOnly(
            'org.junit.jupiter:junit-jupiter-engine:5.1.0'
    )
    // Mockito
    testImplementation "org.mockito:mockito-core:2.21.0"

    //Logging
    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-log4j2
    implementation 'org.springframework.boot:spring-boot-starter-log4j2:2.0.5.RELEASE'

    //implementation 'org.flywaydb:flyway-core:5.1.4'//Useful for migrations
}

//There are more options that we might want to add
//https://www.jooq.org/doc/3.11/manual/getting-started/tutorials/jooq-in-7-steps/jooq-in-7-steps-step3/
jooq {
    version = '3.11.2'
    edition = 'OSS'
    sample(sourceSets.main){
        jdbc {
            //driver = 'com.mysql.cj.jdbc.Driver'
            driver = 'com.mysql.jdbc.Driver'
            url = 'jdbc:mysql://localhost:3306/?useSSL=false&serverTimezone=UTC'//change to true for production
            user = 'user'//Change to your user
            password = 'pass'//Change to your password
            schema = 'fcgl_local'

        }
        generator {
            database {
            name = 'org.jooq.meta.mysql.MySQLDatabase'
            inputSchema = 'fcgl_local'
            includes = '.*'
            }
        }
    }
}
